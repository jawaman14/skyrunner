extends Node
## Entry point (port of skyrunner/__main__.py).
##
##   godot                                   # sandbox, solo
##   godot                                   # the story: Costa Brava 1979-1989, factions unlock chapter by chapter
##   godot -- --unlocks open                 # open world: every faction and mechanic from the start
##   godot -- --tutorial                     # the tutorial: lessons as you play, tips when things happen
##   godot -- --chapter 4                    # the story, skipping ahead to chapter 4 (1982)
##   godot -- --mode campaign                # flying lessons: Palmetto Cay, the classic island
##   godot -- --mode coop                    # host: friends join as co-pilot / spotter
##   godot -- --mode versus                  # host: a friend runs the task-force desk
##   godot -- --police                       # play the task force against AI runners
##   godot -- --players 6                    # seats and rule layers for a table of six
##   godot -- --watch --graphics low         # the AI flies the career; you watch
##   godot -- --map city                     # Costa Brava, the city coast (the default for new games)
##   godot -- --map 42                       # a generated island (0 = the classic one)
##   godot -- --shot out.png --frames 90     # render N frames, save a screenshot, quit
##   godot --headless -- --smoke 600         # run N frames of a new game, print SMOKE OK, quit (CI)
##
## With no arguments the lobby opens, which sets the same options with menus.

var save_dir := "user://"  ## the tests point it elsewhere so they never touch a player's saves

var args := {"mode": "solo", "police": false, "host": false, "port": 47800, "bind": "*", "new": false, "seed": 1,
	"players": 0, "layer": 0, "graphics": "high", "watch": false, "shot": "", "frames": 90, "hour": -1.0,
	"map": -1, "weather": "", "connect": "", "role": "copilot", "name": "player", "seat3d": false, "lobby": true, "unlocks": "story", "chapter": 0, "tutorial": false, "smoke": 0}


func _ready() -> void:
	var look := ControlsConfig.settings()
	UIStyle.set_palette(look.palette)  # neon, or colour-safe (F8 in the 3D seat, or the pause menu)
	PauseMenu.apply_volume(float(look.volume))
	args["graphics"] = str(look.graphics)  # the pause menu's choice; --graphics still wins
	var a := OS.get_cmdline_user_args()
	var i := 0
	while i < a.size():
		var k: String = a[i].trim_prefix("--")
		if args.has(k) and args[k] is bool:
			args[k] = true
		elif args.has(k) and i + 1 < a.size():
			var v: String = a[i + 1]
			if k == "map" and v == "city":
				v = str(MapCity.SEED)
			args[k] = int(v) if args[k] is int else (float(v) if args[k] is float else v)
			i += 1
		else:
			push_warning("unknown argument " + a[i])
		i += 1
	if not a.is_empty():
		args["lobby"] = false
	if args["lobby"]:
		show_lobby()
		return
	start()


func show_lobby() -> void:
	var lobby := Lobby.new()
	add_child(lobby)
	lobby.start.connect(func(opts):
		lobby.queue_free()
		args.merge(opts, true)
		start())


## The pause menu's exits: tear the game down, then reload its save, open the lobby, or quit.
func _leave(to: String) -> void:
	if to == "desktop":
		get_tree().quit()
		return
	for c in get_children():
		if c is PilotApp or c is StationApp or c is HostServer or c is RemoteSeat or c is NetClient:
			remove_child(c)
			c.queue_free()
	Input.mouse_mode = Input.MOUSE_MODE_VISIBLE
	args["new"] = false  # a reload is the save as it is now, never a fresh game
	if to == "load":
		args["graphics"] = str(ControlsConfig.settings().graphics)
		start()
	else:
		show_lobby()


func start() -> void:
	if args["connect"] != "":
		_join()
		return
	var features = null
	if args["players"] or args["layer"]:
		var plan := Layers.plan_match(maxi(1, args["players"]), args["mode"] != "coop", args["layer"])
		print(plan.describe())
		if args["mode"] in ["solo", "coop", "versus"]:
			args["mode"] = plan.mode
		features = Layers.features_for(plan.layer)
		if plan.layer >= 5:
			features["hq"] = true
		features = features.keys()
	if args["police"]:  # offline task-force desk against AI runners
		var ps := Session.new({"mode": Roles.POLICE, "seed": args["seed"], "humans": {Roles.CONTROLLER: args["name"]},
			"map_seed": args["map"] if args["map"] >= 0 else MapCity.SEED})
		var desk := StationApp.new()
		add_child(desk)
		desk.setup(LocalLink.new(ps, Roles.CONTROLLER), Roles.CONTROLLER, ps.world)
		return
	var mode: String = args["mode"]
	var story: bool = args["unlocks"] == "story" and mode != Roles.CAMPAIGN and features == null
	var save := save_dir + ("campaign.json" if mode == Roles.CAMPAIGN else ("story.json" if story else "save.json"))
	if args["new"] and FileAccess.file_exists(save):
		DirAccess.remove_absolute(ProjectSettings.globalize_path(save))
	var opts := {"seed": args["seed"], "mode": mode, "ai_law_upgrades": true, "ground_war": true, "chronicle": true, "agency": true, "family": true, "island": true, "court": true, "payroll": true, "trade": true, "logistics": true, "renown": true, "rackets": true, "races": true, "fog": true}  # the AI chief shops as forfeiture comes in
	if story:  # the chapters build the rest as they open (Story.CHAPTERS)
		for k in Session.SYSTEMS:
			opts.erase(k)
		opts["career"] = true
	elif mode != Roles.CAMPAIGN and features == null:
		opts["money"] = Session.OPEN_FLOAT  # open mode: everything live at once, so a float (a new game only)
	if args["map"] >= 0:  # --map 0 = classic island, --map N = generated island N
		opts["map_seed"] = args["map"]
	elif not FileAccess.file_exists(save):
		opts["map_seed"] = MapCity.SEED  # a new game starts on the city coast; old saves keep their island
	if features != null:
		opts["features"] = features
	var fresh := not FileAccess.file_exists(save)
	var sess := Session.load_or_new(save, opts)
	if fresh and opts.has("money"):
		sess.say("Benny Ruiz fronts the start-up money: $%s. Everything's open from the first minute - spend it well." % Py.money(int(opts.money)))
	if args["weather"] != "":  # --weather clear|cloud|storm[,moon 0..1]; the HQ season sets its own each night
		var wp: PackedStringArray = str(args["weather"]).split(",")
		sess.set_weather({"sky": wp[0], "moon": float(wp[1]) if wp.size() > 1 else 0.5})
	if mode == Roles.CAMPAIGN:
		Campaign.from_dict(Session.read_save(save).get("campaign")).attach(sess)
	elif story:
		var st := Story.from_dict(Session.read_save(save).get("story"))
		st.attach(sess)
		while st.index + 1 < args["chapter"] and not st.completed_all:  # --chapter N: skip ahead
			st.advance()
	# the tutorial: asked for, or still on in this save
	var tut = Session.read_save(save).get("tutorial")
	if args["tutorial"] or (tut is Dictionary and bool(tut.get("on", false)) and not args["new"]):
		Tutorial.new(tut if not args["new"] else null).attach(sess)
		if args["tutorial"]:
			sess.tutorial.enabled = true
	var server = null
	if args["host"] or mode in [Roles.COOP, Roles.VERSUS]:
		server = HostServer.new()
		add_child(server)
		var err = server.start(args["port"], mode if mode != Roles.SOLO else Roles.COOP)
		if err:
			sess.say(err)
			server = null
		else:
			sess.say("Hosting on port %d: friends join with --connect YOUR_IP:%d (--role pick to choose a seat; the AI plays every seat nobody takes)" % [server.port, server.port])
	var bot = null
	if args["watch"]:
		bot = AutoRunner.new(sess)
		sess.say("Watching the AI fly. [C] cycles cameras.")
	var app := PilotApp.new()
	add_child(app)
	app.setup(sess, args["graphics"], bot, server)
	app.leave.connect(_leave)
	if args["hour"] >= 0:
		app.scene.set_hour(args["hour"])
	if args["shot"] != "":
		_shoot(app)
	elif args["smoke"] > 0:
		_smoke(sess)


## Remote seat: a 3D view for the police pilot (and the co-pilot with --seat3d),
## the 2D station for everyone else.
func _join() -> void:
	var a: String = args["connect"]
	var i := a.rfind(":")
	var host := a.substr(0, i) if i > 0 else "127.0.0.1"
	var port := int(a.substr(i + 1)) if i >= 0 else HostServer.DEFAULT_PORT
	var link := NetClient.new()
	add_child(link)
	var role: String = args["role"] if args["role"] != "pick" else ""
	link.open(host, port, args["name"], role)
	if role == "":
		# the live seat list: take whatever the AI is playing
		var picker := SeatPicker.new()
		add_child(picker)
		picker.setup(link)
		picker.seated.connect(func(r):
			picker.queue_free()
			_seat(link, r), CONNECT_ONE_SHOT)
		return
	_seat(link, role)


func _seat(link: NetClient, role: String) -> void:
	if role in [Roles.INTERCEPTOR, Roles.PILOT] or (role == Roles.COPILOT and args["seat3d"]):
		var seat := RemoteSeat.new()
		add_child(seat)
		seat.setup(link, role, args["graphics"])
	else:
		var st := StationApp.new()
		add_child(st)
		st.setup(link, role)


## Run a new game for a while and quit (the release smoke test in CI): the
## output is checked for SCRIPT ERROR, and the game has to reach the end.
func _smoke(sess: Session) -> void:
	for f in args["smoke"]:
		await get_tree().process_frame
	print("SMOKE OK %s t=%.1fs phase=%s money=%s" % [Beta.version(), sess.time, sess.phase, sess.money])
	get_tree().quit()


## Render a few frames and save a screenshot (docs, CI smoke tests).
func _shoot(app: PilotApp) -> void:
	for f in args["frames"]:
		await get_tree().process_frame
	await RenderingServer.frame_post_draw
	var img := get_viewport().get_texture().get_image()
	var path: String = args["shot"]
	img.save_png(path if path.is_absolute_path() else ProjectSettings.globalize_path("res://").path_join(path))
	print("saved ", path)
	get_tree().quit()
